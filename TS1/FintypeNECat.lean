import Mathlib.Tactic 
import Mathlib.CategoryTheory.FintypeCat
import Mathlib.CategoryTheory.ConcreteCategory.BundledHom
import Mathlib.Order.Category.NonemptyFinLinOrdCat



universe u v 

/- This is basically a copy-paste of FintypeCat, adapted to nonempty finite types.-/

/-!
# The category of nonempmty finite types.

We define the category of nonempty finite types, denoted `FintypeNECat` as
(bundled) types with `Nonempty` and `Fintype` instances.

We also define `FintypeNECat.Skeleton`, the standard skeleton of `FintypeNECat` whose objects
are `Fin (n + 1)` for `n : â„•`. We prove that the obvious inclusion functor
`FintypeNECat.Skeleton â¥¤ FintypeNECat` is an equivalence of categories in
`FintypeNECat.Skeleton.equivalence`.
We prove that `FintypeNECat.Skeleton` is a skeleton of `FintypeNECat` in `FintypeNECat.isSkeleton`.
-/


open Classical

/-- A typeclass for nonempty fintypes. -/
class FintypeNE (Î± : Type _) extends Fintype Î± where
  Nonempty : Nonempty Î± := by infer_instance 

attribute [instance] FintypeNE.Nonempty


def FintypeNE.toType (Î± : Type _) [FintypeNE Î±] : Type _ := Î±  


instance PUnit.fintypeNE : FintypeNE PUnit where

instance Unit.fintypeNE : FintypeNE Unit where


instance Fin.fintypeNE (n : â„•) : FintypeNE (Fin (n + 1)) where

instance ULift.fintypeNE (Î± : Type u) [FintypeNE Î±] :
    FintypeNE (ULift.{v} Î±) :=
  { ULift.fintype _ with }


noncomputable def FintypeNE.equivFin (Î±) [FintypeNE Î±] : Î± â‰ƒ Fin ((Fintype.card Î±).pred + 1) := by
  rw [â†Nat.succ_eq_add_one, Nat.succ_pred_eq_of_pos]
  . exact Fintype.equivFin Î± 
  . exact Fintype.card_pos  

open CategoryTheory

/-- The category of nonempty finite types. -/
def FintypeNECat :=
  Bundled FintypeNE 


namespace FintypeNECat

instance : CoeSort FintypeNECat (Type _) :=
  Bundled.coeSort

/-- Construct a bundled `FintypeNECat` from the underlying type and typeclass. -/
def of (X : Type _) [FintypeNE X] : FintypeNECat :=
  Bundled.of X


instance : Inhabited FintypeNECat :=
  âŸ¨of PUnitâŸ©

instance {X : FintypeNECat} : Fintype X.1 :=
  X.2.toFintype  

instance : Category FintypeNECat :=
  InducedCategory.category Bundled.Î±


/-- The fully faithful embedding of `FintypeNECat` into the category of types. -/
@[simps!]
def incl : FintypeNECat â¥¤ Type _ :=
  inducedFunctor _ 

instance : Full incl := InducedCategory.full _
instance : Faithful incl := InducedCategory.faithful _

instance concreteCategoryFintypeNE : ConcreteCategory FintypeNECat :=
  âŸ¨inclâŸ©


@[simp]
theorem id_apply (X : FintypeNECat) (x : X) : (ðŸ™ X : X â†’ X) x = x :=
  rfl

@[simp]
theorem comp_apply {X Y Z : FintypeNECat} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) (x : X) : (f â‰« g) x = g (f x) :=
  rfl

-- porting note: added to ease automation
@[ext]
lemma hom_ext {X Y : FintypeNECat} (f g : X âŸ¶ Y) (h : âˆ€ x, f x = g x): f = g := by
  funext
  apply h

-- See `equivEquivIso` in the root namespace for the analogue in `Type`.
/-- Equivalences between nonempty finite types are the same as isomorphisms in `FintypeNECat`. -/
@[simps]
def equivEquivIso {A B : FintypeNECat} : A â‰ƒ B â‰ƒ (A â‰… B) where
  toFun e :=
    { hom := e
      inv := e.symm }
  invFun i :=
    { toFun := i.hom
      invFun := i.inv
      left_inv := congr_fun i.hom_inv_id
      right_inv := congr_fun i.inv_hom_id }
  left_inv := by aesop_cat
  right_inv := by aesop_cat


/--
The "standard" skeleton for `FintypeNECat`. This is the full subcategory of `FintypeNECat`
spanned by objects of the form `ULift (Fin (n + 1_)` for `n : â„•`. We parametrize the objects
of `FintypeNE.Skeleton` directly as `ULift â„•`, as the type `ULift (Fin (m + 1)) â‰ƒ ULift (Fin (n + 1))`
is nonempty if and only if `n = m`. Specifying universes, `Skeleton : Type u` is a small
skeletal category equivalent to `FintypeNE.{u}`.
-/
def Skeleton : Type u :=
  ULift â„•

namespace Skeleton

/-- Given any natural number `n`, this creates the associated object of `FintypeNE.Skeleton`. -/
def mk : â„• â†’ Skeleton :=
  ULift.up

instance : Inhabited Skeleton :=
  âŸ¨mk 0âŸ©

/-- Given any object of `FintypeNE.Skeleton`, this returns the associated natural number. -/
def len : Skeleton â†’ â„• :=
  ULift.down

@[ext]
theorem ext (X Y : Skeleton) : X.len = Y.len â†’ X = Y :=
  ULift.ext _ _

instance : SmallCategory Skeleton.{u} where
  Hom X Y := ULift.{u} (Fin (X.len + 1)) â†’ ULift.{u} (Fin (Y.len + 1))
  id _ := id
  comp f g := g âˆ˜ f

theorem is_skeletal : Skeletal Skeleton.{u} := fun X Y âŸ¨hâŸ© => 
  ext _ _ <|
   Nat.succ_injective <|
    Fin.equiv_iff_eq.mp <|
      Nonempty.intro <|
        { toFun := fun x => (h.hom âŸ¨xâŸ©).down
          invFun := fun x => (h.inv âŸ¨xâŸ©).down
          left_inv := by
            intro a
            change ULift.down _ = _
            rw [ULift.up_down]
            change ((h.hom â‰« h.inv) _).down = _
            simp
            rfl
          right_inv := by
            intro a
            change ULift.down _ = _
            rw [ULift.up_down]
            change ((h.inv â‰« h.hom) _).down = _
            simp
            rfl }


/-- The canonical fully faithful embedding of `FintypeNE.Skeleton` into `FintypeNECat`. -/
def incl : Skeleton.{u} â¥¤ FintypeNECat.{u} where
  obj X := FintypeNECat.of (ULift (Fin (X.len + 1)))
  map f := f

instance : Full incl where preimage f := f

instance : Faithful incl where


instance : EssSurj incl :=
  EssSurj.mk fun X =>
    let F := @FintypeNE.equivFin X.1 X.2 
    âŸ¨mk (Fintype.card X).pred,
      Nonempty.intro
        { hom := F.symm âˆ˜ ULift.down
          inv := ULift.up âˆ˜ F }âŸ©

noncomputable instance : IsEquivalence incl :=
  Equivalence.ofFullyFaithfullyEssSurj _

/-- The equivalence between `Fintype.Skeleton` and `FintypeNE`. -/
noncomputable def equivalence : Skeleton â‰Œ FintypeNECat :=
  incl.asEquivalence


@[simp]
theorem incl_mk_nat_card (n : â„•) : Fintype.card (incl.obj (mk n)) = n + 1 := by
  convert Finset.card_fin (n+1) 
  apply Fintype.ofEquiv_card

end Skeleton

/-- `FintypeNE.Skeleton` is a skeleton of `FintypeNE`. -/
noncomputable def isSkeleton : IsSkeletonOf FintypeNECat Skeleton Skeleton.incl where
  skel := Skeleton.is_skeletal
  eqv := by infer_instance

end FintypeNECat
